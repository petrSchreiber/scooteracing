uses "tbgl", "tbdi", "dt"

#include once "data/game/resourceManager.tbasicu"
#include once "offScreenRenderer/offScreenRenderer8b.tbasicu"
#include once "controllers/xboxOne.tbasicu"
#include once "data/game/track.tbasicu"
#include once "racers/scooter.tbasicu"

#include once "modes/game.tbasicu"
#include once "modes/menu.tbasicu"

function tbMain()
  
  double frameRate
  
  int32 ScreenWidth = 800
  int32 ScreenHeight= 600
  
  ' -- Create and show window
  dWord hWnd = tbgl_createWindowEx("Scooteracing - press ESC to quit", ScreenWidth, ScreenHeight, 32, %TBGL_WS_WINDOWED | %TBGL_WS_CLOSEBOX)
  tbgl_showWindow

  ' -- Controllers
  dim playerController(2) as tXBoxOneController
  playerController(1).Initialize(hWnd, 1)
  playerController(2).Initialize(hWnd, 2)
  
  ' -- Rendering
  dim osr(2) as offScreenRenderer8b
  long width, height
  tbgl_getWindowClient(hWnd, width, height)
  osr(1).buildBuffer(width/4, height/4, %OSR_NEAREST_NEIGHBOUR)
  osr(2).buildBuffer(width/4, height/4, %OSR_NEAREST_NEIGHBOUR)
  tbgl_backColor 64, 128, 255
  
  ' -- Fonts
  TBGL_BuildFont(TBGL_FontHandle("Arial Black", 24), 1)
  TBGL_BuildFont(TBGL_FontHandle("Arial Black", 8), 2)
  TBGL_BuildFont(TBGL_FontHandle("Arial Black", 18, %TBGL_BOLD | %TBGL_ITALICS), 3)

  ' -- Game
  dim game as tGameSession
  dim mainMenu as tMenuSession
  
  %MODE_MENU = 1
  %MODE_GAME = 2
  dim mode = %MODE_MENU

  select case mode
    case %MODE_MENU
      mainMenu.Initialize(1, 1, 1)
      
    case %MODE_GAME
      game.Initialize("Curly.ini", "naranjino.ini", "medlina.ini")

  end select
  
  while tbgl_isWindow(hWnd)
  
    frameRate = tbgl_getFrameRate
    if rndf(0,1) > 0.85 then tbgl_setWindowTitle(hWnd, format$(frameRate, "#.00"))

    select case mode
      case %MODE_MENU
        mainMenu.Render(osr, frameRate)
        mainMenu.ProcessInput(playerController, frameRate)
        
        if mainMenu.ready(1) = true and mainMenu.ready(2) = true then
          mainMenu.ReleaseResources()
          game.Initialize(mainMenu.GetTrack(), mainMenu.GetRacer1(), mainMenu.GetRacer2())
          mode = %MODE_GAME
        end if
        
      case %MODE_GAME
        game.ProcessInput(playerController)

        game.HandleCollisions(frameRate)
        
        game.EvaluateRaceFinish(frameRate)
        
        game.Render(osr, frameRate)
        
        if game.ready(1) = true and game.ready(2) = true then
          game.ReleaseResources()
          mainMenu.Initialize(mainMenu.trackNow, mainMenu.racerNow(1), mainMenu.racerNow(2))
          mode = %MODE_MENU
        end if
        
    end select
      
    if tbgl_getWindowKeyState(hWnd, %VK_ESCAPE) then exit while
    
  wend

  tbgl_destroyWindow
end function


