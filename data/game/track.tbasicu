uses "ui", "ini"
uses "console"

#include once "resourceManager.tbasicu"

type tTrack

  mRoad  as int32
  mSign      as int32
  mTerrain   as int32
  mTerrainDecal   as int32
  
  dlTrack    as int32
  
  startX     as single
  startZ     as single
  startRot   as single

  finishX     as single
  finishZ     as single

  hBitmap    as UInt32
  nWidth     as uint32
  nHeight    as uint32
  
  record as string
  
  configFile  as string
  
  function private_IsRoad(x as int32, y as int32) as int32
    return in(Canvas_GetPixel(x, y), %COLOR_ROAD, %COLOR_START, %COLOR_FINISH) > 0
  end function
  
  function Load(configFile as string)
    me.configFile = configFile
    string modelPath = app_sourcepath+"data\Models\"

    me.ReleaseResources()
    
    me.record = INI_GetKey(configFile, "Metadata", "record")
    
    ' -- Load resources
    me.mRoad          = gResourceManager.GetModelId()
    me.mSign          = gResourceManager.GetModelId()
    me.mTerrain       = gResourceManager.GetModelId()
    me.mTerrainDecal  = gResourceManager.GetModelId()
    
    me.dlTrack        = gResourceManager.GetDListId()
    
    tbgl_m15LoadModel modelPath + INI_GetKey(configFile, "Tiles", "road")         , "",         me.mRoad, 0, %TBGL_NORMAL_PRECISE
    tbgl_m15LoadModel modelPath + INI_GetKey(configFile, "Tiles", "sign")         , "",         me.mSign, 0, %TBGL_NORMAL_PRECISE
    tbgl_m15LoadModel modelPath + INI_GetKey(configFile, "Tiles", "terrain")      , "",      me.mTerrain, 0, %TBGL_NORMAL_PRECISE
    tbgl_m15LoadModel modelPath + INI_GetKey(configFile, "Tiles", "terrain_decal"), "", me.mTerrainDecal, 0, %TBGL_NORMAL_PRECISE
    
    ' -- Create display list of track
    string trackBitmap = app_sourcepath+"data\Tracks\" + INI_GetKey(configFile, "Map", "bitmap")
    Canvas_BitmapGetFileInfo(trackBitmap, me.nWidth, me.nHeight)
    me.hBitmap = Canvas_BitmapLoad(trackBitmap, me.nWidth, me.nHeight)
    Canvas_Attach(me.hBitmap, 0, 0)
    
    %COLOR_TERRAIN = rgb(34, 177, 76)
    %COLOR_TERRAIN_TREE = rgb(181, 230, 29)
    %COLOR_ROAD    = rgb(195, 195, 195)
    %COLOR_START   = rgb(255, 127, 39)
    %COLOR_FINISH  = rgb(237, 28, 36)
    int32 x, y
    single tileX, tileZ
    tbgl_newList me.dlTrack
    for x = 1 to me.nWidth
      for y = 1 to me.nHeight
        
        tileX = x*8-(me.nWidth*8)/2
        tileZ = y*8-(me.nHeight*8)/2
        tbgl_pushMatrix
          tbgl_translate tileX, 0, tileZ
         
          if Canvas_GetPixel(x, y) = %COLOR_TERRAIN then
              tbgl_m15DrawModel me.mTerrain

          elseif Canvas_GetPixel(x, y) = %COLOR_TERRAIN_TREE then
              tbgl_rotate 0, rnd(0,360),0'*90,0
              tbgl_m15DrawModel me.mTerrainDecal

          elseif Canvas_GetPixel(x, y) = %COLOR_ROAD then
              tbgl_m15DrawModel me.mRoad
          
          elseif Canvas_GetPixel(x, y) = %COLOR_START then
            
            if Canvas_GetPixel(x+1, y) = %COLOR_ROAD then
              me.startRot = 90
            elseif Canvas_GetPixel(x, y+1) = %COLOR_ROAD then
              me.startRot = 180
            elseif Canvas_GetPixel(x-1, y) = %COLOR_ROAD then
              me.startRot = 270
            else
              me.startRot = 0
            end if
            
            tbgl_rotate me.startRot, 0, 1, 0
            
            tbgl_m15DrawModel me.mSign
            
            me.startX = tileX
            me.startZ = tileZ
            
          elseif Canvas_GetPixel(x, y) = %COLOR_FINISH then
            if Canvas_GetPixel(x+1, y) = %COLOR_ROAD then
              tbgl_rotate 90, 0, 1, 0
            elseif Canvas_GetPixel(x-1, y) = %COLOR_ROAD then
              tbgl_rotate 270, 0, 1, 0
            end if
              
            tbgl_m15DrawModel me.mSign

            me.finishX = tileX
            me.finishZ = tileZ
            
          end if
        tbgl_popMatrix

      next
  
    next
    
    tbgl_endList
    
    return me.dlTrack
  end function
  
  function isTerrain(worldX as single, worldZ as single)
    Canvas_Attach(me.hBitmap, 0, 0)
    int32 x = ((me.nWidth*8)/2 + worldX)/8
    int32 y = ((me.nHeight*8)/2 + worldZ)/8
    
    return not in(Canvas_GetPixel(x, y), %COLOR_ROAD, %COLOR_START, %COLOR_FINISH)
  end function
  
  function SetRecord(newRecord as string)
    me.record = newRecord
    INI_SetKey(me.configFile, "Metadata", "record", newRecord)
  end function
  
  function ReleaseResources()
    if me.mRoad         then gResourceManager.FreeModelId(me.mRoad)
    if me.mSign         then gResourceManager.FreeModelId(me.mSign)
    if me.mTerrain      then gResourceManager.FreeModelId(me.mTerrain)
    if me.mTerrainDecal then gResourceManager.FreeModelId(me.mTerrainDecal)
    if me.dlTrack       then gResourceManager.FreeDListId(me.dlTrack)
    
    me.mRoad = 0
    me.mSign = 0
    me.mTerrain = 0
    me.mTerrainDecal = 0
    me.dlTrack = 0
  end function

end type
